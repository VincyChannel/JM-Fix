buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://maven.minecraftforge.net/" }
        maven {
            url = "https://repo.spongepowered.org/maven"
        }
    }

    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+"
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle"
apply plugin: 'org.spongepowered.mixin'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
}

repositories {
    maven {
        url = "https://maven.minecraftforge.net/"
    }
    maven {
        url = "https://repo.spongepowered.org/maven"
    }
    maven {
        url 'https://maven.cleanroommc.com'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2859"

    implementation 'org.spongepowered:mixin:0.8.5'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    implementation name: 'journeymap-1.12.2-5.7.1p3'

    testAnnotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    //annotationProcessor 'org.ow2.asm:asm-debug-all:5.2'
    //annotationProcessor 'com.google.guava:guava:32.1.2-jre'
    //annotationProcessor 'com.google.code.gson:gson:2.8.9'
    //implementation ('zone.rong:mixinbooter:10.0') {
    //    transitive = false
    //}
    //annotationProcessor ('zone.rong:mixinbooter:10.0') {
    //    transitive = false
    //}
}

mixin {
    config("mixins.journeymapfix.json")
    defaultObfuscationEnv("notch")
}

minecraft {
    mappings channel: "stable", version: "39-1.12"

    runs {
        "client" {
            workingDirectory file("./run")
            mods { "${project.name}" { source sourceSets.main } }
        }

        "server" {
            workingDirectory file("./run/server")
            mods { "${project.name}" { source sourceSets.main } }
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("mcmod.info") {
        expand "version": project.version
    }
}

jar {
    manifest.attributes(
            'ForceLoadAsMod': 'true',
            'FMLCorePluginContainsFMLMod': 'true',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
    )
}

//When Forge 1.12 loads mods from a directory that's been put on the classpath, it expects to find resources in the same directory.
//Default Gradle behavior puts resources in ./build/resources/main instead of ./build/classes/main/java. Let's change that.
sourceSets.all { it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next() }